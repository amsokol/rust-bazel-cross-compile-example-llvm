"""Configuration for Rust toolchains and dependencies.

This module sets up the Rust toolchain, LLVM toolchain, and required crates
for Rust service development.
"""

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

# INTEL/AMD64 Sysroot.
http_archive(
    name = "sysroot_linux_amd64",
    build_file = "//:BUILD.sysroot",
    sha256 = "2db6f4172fe228eceefb1d7fc5cf3b28049bbbf30cde22da9dc74d2780c8b4b9",
    urls = ["https://github.com/amsokol/cross-compilation-sysroots/releases/download/2026-01-13/debian-trixie-sysroot-amd64.tar.xz"],
)

# ARM 64 Sysroot.
http_archive(
    name = "sysroot_linux_arm64",
    build_file = "//:BUILD.sysroot",
    sha256 = "f608350bba1f26db700c6c9dc3739648e23920b0831f839bf2a1708501c07142",
    urls = ["https://github.com/amsokol/cross-compilation-sysroots/releases/download/2026-01-13/debian-trixie-sysroot-arm64.tar.xz"],
)

llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm", dev_dependency = True)
llvm.toolchain(
    name = "llvm_toolchain",
    llvm_version = "21.1.8",
    extra_llvm_distributions = {
        "LLVM-21.1.8-Linux-ARM64.tar.xz": "65ce0b329514e5643407db2d02a5bd34bf33d159055dafa82825c8385bd01993",
        "LLVM-21.1.8-Linux-X64.tar.xz": "b3b7f2801d15d50736acea3c73982994d025b01c2f035b91ae3b49d1b575732b",
        "LLVM-21.1.8-macOS-ARM64.tar.xz": "b95bdd32a33a81ee4d40363aaeb26728a26783fcef26a4d80f65457433ea4669",
    },
)

llvm.sysroot(
    name = "llvm_toolchain",
    label = "@sysroot_linux_amd64//:sysroot",
    targets = ["linux-x86_64"],
)
llvm.sysroot(
    name = "llvm_toolchain",
    label = "@sysroot_linux_arm64//:sysroot",
    targets = ["linux-aarch64"],
)
use_repo(llvm, "llvm_toolchain")

register_toolchains(
    "@llvm_toolchain//:all",
    dev_dependency = True,
)

RUST_EDITION = "2024"

RUST_VERSION = "1.93.0"

rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust.toolchain(
    edition = RUST_EDITION,
    extra_target_triples = [
        "aarch64-unknown-linux-gnu",
        "x86_64-unknown-linux-gnu",
    ],
    versions = [RUST_VERSION],
)
use_repo(rust, "rust_toolchains")

register_toolchains("@rust_toolchains//:all")

cargo = use_extension(
    "@rules_rust//crate_universe:extensions.bzl",
    "crate",
)
cargo.from_cargo(
    name = "cargo",
    cargo_lockfile = "//:Cargo.lock",
    manifests = [
        "//:Cargo.toml",
        "rust/hello_world/Cargo.toml",
    ],
)
use_repo(cargo, "cargo")
