"""Configuration for Rust toolchains and dependencies.

This module sets up the Rust toolchain, LLVM toolchain, and required crates
for Rust service development.
"""

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

# INTEL/AMD64 Sysroot.
http_archive(
    name = "sysroot_linux_amd64",
    build_file = "//:BUILD.sysroot",
    sha256 = "bb19e1ac5d1adc6e3ca73ce042957627ea4cef4062ce8252a98b9bccd30329f2",
    urls = ["https://github.com/amsokol/cross-compilation-sysroots/releases/download/2025-12-30/debian-trixie-sysroot-amd64.tar.xz"],
)

# ARM 64 Sysroot.
http_archive(
    name = "sysroot_linux_arm64",
    build_file = "//:BUILD.sysroot",
    sha256 = "a603869b0967b3748defa345be47662286a1602eec0871e9fc6b77bd1556a2dc",
    urls = ["https://github.com/amsokol/cross-compilation-sysroots/releases/download/2025-12-30/debian-trixie-sysroot-arm64.tar.xz"],
)

# https://github.com/bazel-contrib/toolchains_llvm/blob/master/toolchain/internal/llvm_distributions.bzl
llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm", dev_dependency = True)
llvm.toolchain(
    name = "llvm_toolchain",
    # Use LLVM version corresponded to LLVM is embedded into Rust toolchain.
    # Rust 1.92 embeds LLVM 21.1.3
    llvm_versions = {
        "darwin-aarch64": "21.1.8",
        "linux-aarch64": "21.1.8",
        "linux-x86_64": "21.1.8",
    },
)
llvm.sysroot(
    name = "llvm_toolchain",
    label = "@sysroot_linux_amd64//:sysroot",
    targets = ["linux-x86_64"],
)
llvm.sysroot(
    name = "llvm_toolchain",
    label = "@sysroot_linux_arm64//:sysroot",
    targets = ["linux-aarch64"],
)
use_repo(llvm, "llvm_toolchain")

register_toolchains(
    "@llvm_toolchain//:all",
    dev_dependency = True,
)

RUST_EDITION = "2024"

RUST_VERSION = "1.92.0"

rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust.toolchain(
    edition = RUST_EDITION,
    extra_target_triples = [
        "aarch64-unknown-linux-gnu",
        "x86_64-unknown-linux-gnu",
    ],
    versions = [RUST_VERSION],
)
use_repo(rust, "rust_toolchains")

register_toolchains("@rust_toolchains//:all")

cargo = use_extension(
    "@rules_rust//crate_universe:extensions.bzl",
    "crate",
)
cargo.from_cargo(
    name = "cargo",
    cargo_lockfile = "//:Cargo.lock",
    manifests = [
        "//:Cargo.toml",
        "rust/hello_world/Cargo.toml",
    ],
)
use_repo(cargo, "cargo")
